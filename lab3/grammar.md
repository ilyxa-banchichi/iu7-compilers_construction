## Исходная грамматика выражений

<программа> ->
    <блок>

<блок> ->
    { <список операторов> }

<список операторов> ->
    <оператор> <список операторов хвост>

<список операторов хвост> ->
    ; <оператор> <список операторов хвост> | ε

<оператор> ->
    <идентификатор> = <выражение> |
    <блок>

<выражение> ->
    <арифметическое выражение> <операция отношения> <арифметическое выражение> |
    <арифметическое выражение>

<арифметическое выражение> ->
    <арифметическое выражение> <операция типа сложения> <терм> |
    <терм>

<терм> ->
    <терм> <операция типа умножения> <фактор> |
    <фактор>

<фактор> ->
    <идентификатор> |
    <константа> |
    ( <арифметическое выражение> )

<операция отношения> ->
    < | <= | = | <> | > | >=

<операция типа сложения> ->
    + | -

<операция типа умножения> ->
    * | /

## Грамматика выражений без левой рекурсии

<программа> ->
    <блок>

<блок> ->
    { <список операторов> }

<список операторов> ->
    <оператор> <список операторов хвост>

<список операторов хвост> ->
    ; <оператор> <список операторов хвост> | ε

<оператор> ->
    <идентификатор> = <выражение> |
    <блок>

<выражение> ->
    <арифметическое выражение> <выражение хвост>

<выражение хвост> ->
    <операция отношения> <арифметическое выражение> |
    ε

<арифметическое выражение> ->
    <терм> <арифметическое выражение хвост>

<арифметическое выражение хвост> ->
    <операция типа сложения> <терм> <арифметическое выражение хвост> |
    ε

<терм> ->
    <фактор> <терм хвост>

<терм хвост> ->
    <операция типа умножения> <фактор> <терм хвост> |
    ε

<фактор> ->
    ( <арифметическое выражение> ) |
    <идентификатор> |
    <константа>

<операция отношения> ->
    < |
    <= |
    <> |
    == |
    > |
    >=

<операция типа сложения> ->
    + |
    -

<операция типа умножения> ->
    * |
    /