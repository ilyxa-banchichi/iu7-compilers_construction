## Исходная грамматика программы

<программа> ->
    <блок>

<блок> ->
    { <список_операторов> }

<список_операторов> ->
    <оператор> <список_операторов_хвост>

<список_операторов_хвост> ->
    ; <оператор> <список_операторов_хвост> | ε

<оператор> ->
    <идентификатор> = <выражение> |
    <блок>

<выражение> ->
    <арифметическое_выражение> <операция_отношения> <арифметическое_выражение> |
    <арифметическое_выражение>

<арифметическое_выражение> ->
    <арифметическое_выражение> <операция_типа_сложения> <терм> |
    <терм>

<терм> ->
    <терм> <операция_типа_умножения> <фактор> |
    <фактор>

<фактор> ->
    <идентификатор> |
    <константа> |
    ( <арифметическое_выражение> )

<операция_отношения> ->
    < | <= | = | <> | > | >=

<операция_типа_сложения> ->
    + | -

<операция_типа_умножения> ->
    * | /

## Грамматика выражений без левой рекурсии

<программа> ->
    <блок>

<блок> ->
    { <список_операторов> }

<список_операторов> ->
    <оператор> <список_операторов_хвост>

<список_операторов_хвост> ->
    ; <оператор> <список_операторов_хвост> | ε

<оператор> ->
    <идентификатор> = <выражение> |
    <блок>

<выражение> ->
    <арифметическое_выражение> <выражение_хвост>

<выражение_хвост> ->
    <операция_отношения> <арифметическое_выражение> |
    ε

<арифметическое_выражение> ->
    <терм> <арифметическое_выражение_хвост>

<арифметическое_выражение_хвост> ->
    <операция_типа_сложения> <терм> <арифметическое_выражение_хвост> |
    ε

<терм> ->
    <фактор> <терм_хвост>

<терм_хвост> ->
    <операция_типа_умножения> <фактор> <терм_хвост> |
    ε

<фактор> ->
    ( <арифметическое_выражение> ) |
    <идентификатор> |
    <константа>

<операция_отношения> ->
    < |
    <= |
    <> |
    == |
    > |
    >=

<операция_типа_сложения> ->
    + |
    -

<операция_типа_умножения> ->
    * |
    /